apply plugin: 'com.android.application'

//코틀린 안드로이드 플러그인을 적용한다.
apply plugin: 'kotlin-android'

//코틀린 안드로이드 익스텐션 플러그인을 적용한다.
apply plugin: 'kotlin-android-extensions'

//코틀린 코드에서 어노테이션 프로세싱을 사용할 수 있도록 kotlin-kapt 플러그인을 추가로 적용한다.
apply plugin: 'kotlin-kapt'

android {
    //dependencies.gradle에 정의된 값을 참조한다.
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    sourceSets{
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.androidhuman.example.simplegithub"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "GITHUB_CLIENT_ID",
                project.properties["GITHUB_CLIENT_ID"] ?: "\"NOT_FOUND\""

        buildConfigField "String", "GITHUB_CLIENT_SECRET",
                project.properties["GITHUB_CLIENT_SECRET"] ?: "\"NOT_FOUND\""

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"

    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    //코틀린 표준 라이브러리를 의존성에 추가한다.
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    //코틀린 어노테이션 프로세서에서 처리될 수 있도록 의존성에 추가한다.
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    //Anko Commons 라이브러리를 의존성에 추가한다.
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"

    //Retrofit에서 받은 응답을 옵저버블로 변환해주는 라이브러리를 추가한다.
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    //RxAndroid와 RxJava 라이브러리를 추가한다.
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    //예제 프로젝트의 SearchView는 appcompat-v7에서 제공하는 라이브러리이고 이에 대응하는 RxBinding 라이브러리를 의존성에 추가한다.
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxBindingVersion"

    //rxbinding-appcompat-v7의 코틀린 확장 라이브러리를 사용한다.
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxBindingVersion"

    //안드로이드 아키텍처 컴포넌트를 위한 의존성을 추가한다.
    implementation "android.arch.lifecycle:extensions:$archComponentVersion"

    //안드로이드 아키텍처 컴포넌트에서는 어노테이션 프로세서를 사용하므로, 이를 위한 의존성도 함께 추가한다.
    annotationProcessor "android.arch.lifecycle:compiler:$archComponentVersion"

    //안드로이드 아키텍처 컴포넌트에서는 어노테이션 프로세서를 사용하므로, 이를 위한 의존성도 함께 추가한다.
    kapt "android.arch.lifecycle:compiler:$archComponentVersion"

    //룸 라이브러리를 추가한다.
    implementation "android.arch.persistence.room:runtime:$archComponentVersion"

    //룸 라이브러리 RxJava 플러그인을 추가한다.
    implementation "android.arch.persistence.room:rxjava2:$archComponentVersion"

    //룸 라이브러리용 어노테이션 프로세서를 추가한다.
    kapt "android.arch.persistence.room:compiler:$archComponentVersion"

    //대거의 기본 기능을 사용하기 위해 필요하다.
    implementation "com.google.dagger:dagger:$daggerVersion"

    //안드로이드에 특화된 대거의 기능을 사용하기 위해 필요하다.
    implementation "com.google.dagger:dagger-android:$daggerVersion"

    //안드로이드에 서포트 라이브러리를 지원하는 대거의 기능을 사용하기 위해 필요하다.
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"

    //안드로이드에 특화된 대거의 기능을 처리해주는 어노테이션 프로세서.
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    //대거의 기본 기능(의존관계 분석 및 코드 생성)을 처리해주는 어노테이션 프로세서.
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
}
